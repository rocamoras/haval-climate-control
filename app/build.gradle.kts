import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
}

android {
    namespace = "br.com.redesurftank.havalclimatecontrol"
    compileSdk = 36

    defaultConfig {
        applicationId = "br.com.redesurftank.havalclimatecontrol"
        minSdk = 28
        //noinspection ExpiredTargetSdkVersion
        targetSdk = 28
        versionCode = 57
        versionName = "1.23.3"
    }

    signingConfigs {
        create("release") {
            storeFile = file("release.keystore")
            storePassword = System.getenv("SIGNING_STORE_PASSWORD")
            keyAlias = System.getenv("SIGNING_KEY_ALIAS")
            keyPassword = System.getenv("SIGNING_KEY_PASSWORD")
        }
    }

    buildTypes {
        named("release") {
            signingConfig = signingConfigs.getByName("release")
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    buildFeatures {
        aidl = true
        compose = true
    }
}

/**
 * Um byte de controle dentro de um .js faz o parser do Frida abortar o script
 * INTEIRO, e isso só aparece no log da injeção — nem no build, nem na UI.
 * (haval-engine-reverse/docs/HANDOFF-cards-midia-online.md §7.1)
 */
val checkFridaScripts = tasks.register("checkFridaScripts") {
    val rawDir = layout.projectDirectory.dir("src/main/res/raw").asFile
    doLast {
        val problems = (rawDir.listFiles() ?: emptyArray())
            .filter { it.name.endsWith(".js") }
            .mapNotNull { f ->
                val idx = f.readBytes().indexOfFirst {
                    val v = it.toInt() and 0xFF
                    v < 9 || v in 11..12 || v in 14..31
                }
                if (idx >= 0) "${f.name}: byte de controle no offset $idx" else null
            }
        if (problems.isNotEmpty()) {
            throw GradleException(
                "script Frida com byte de controle (o parser do Frida aborta): " +
                    problems.joinToString("; ")
            )
        }
    }
}
tasks.named("preBuild") { dependsOn(checkFridaScripts) }

kotlin {
    jvmToolchain(11)
    compilerOptions {
        jvmTarget = JvmTarget.JVM_11
    }
}

dependencies {
    implementation(libs.appcompat)
    implementation(libs.material)
    implementation(libs.shizuku)
    implementation(libs.shizuku.provider)
    implementation(libs.hiddenapibypass)
    implementation(libs.commons.net)
    implementation(libs.lifecycle.runtime.ktx)
    implementation(libs.activity.compose)
    implementation(platform(libs.compose.bom))
    implementation(libs.ui)
    implementation(libs.ui.graphics)
    implementation(libs.ui.tooling.preview)
    implementation(libs.material3)
    implementation("androidx.compose.material:material-icons-extended")
    annotationProcessor(libs.annotation.processor)
    compileOnly(libs.annotation)
    debugImplementation(libs.ui.tooling)
    debugImplementation(libs.ui.test.manifest)
}
